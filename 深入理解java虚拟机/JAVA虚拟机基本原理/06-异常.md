#### java异常概要

##### 两大要素

- 抛出异常
- 捕获异常

两大要素实现了程序的非正常转移。



##### 两种方式

- 显式抛出
  - 应用程序
  - throw关键字
- 隐式抛出
  - jvm
  - 如数组越界，会自动抛出异常



##### 三个代码块

- try
  - 标记下面的代码需要被异常监控
- catch
  - 跟在try块之后
  - 可以有多个
  - 由上到下挨个匹配
  - 上面的异常的范围不能比下面的大
- finally
  - 跟在try块之后，如果有catch，跟在catch块之后
  - 要执行一部分清理的代码，必须执行，但是不一定能执行完，如果finally块中发生了异常，还是会退出的
  - try块和catch块的return不影响finally块的执行



##### 异常分类

- 检查型异常
  - 程序必须给出处理
    - throws
    - catch
- 非检查型
  - Error
    - 这种属于严重问题，程序无需也没办法处置这一种异常
  - Runtime Exception
    - 运行时异常，这个编译时没办法知道

检查和非检查是指编译器能否检查的出来 --> 我们自定义异常的时候都定义成检查型异常，这样可以在最早的时候处理这种异常。



##### 异常构造

###### 轨迹栈(stack trace)

- 会逐一访问当前线程的Java栈帧，记录调试信息
  - 栈帧指向的方法的名称
  - 方法所在的类名、文件名
  - 代码的第几行触发了异常
- 新建异常的方法不会出现在轨迹栈中
- 标记为不可见的方法不会出现在轨迹栈中
- 轨迹栈的优化
  - 缓存异常实例
    - 异常实例的轨迹栈如何缓存呢？
    - 一个方法可以有不同的调用链，同样一个catch块中能抛出一个异常的地方可能不止一处
    - 所以这个优化带来的代价必然是不准确，或者内存和cpu的耗费都大
      - 内存耗费大的原因是因为上面说的类型如果要兼顾到的话，需要缓存很多的实例
      - cpu耗费大的原因是需要做比较，判断实例是否已经存在



#### 虚拟机如何捕获异常

##### 异常表

- 每个方法都有一个
- 一条记录代表一个异常处理器
  - from指针
  - to指针
  - target指针
  - 要捕获的异常类型
- 指针的内容是字节码索引，用以定位字节码
- [from,to) --> try块
- target --> catch块

```java


public static void main(String[] args) {
  try {
    mayThrowException();
  } catch (Exception e) {
    e.printStackTrace();
  }
}
// 对应的Java字节码
public static void main(java.lang.String[]);
  Code:
    0: invokestatic mayThrowException:()V
    3: goto 11
    6: astore_1
    7: aload_1
    8: invokevirtual java.lang.Exception.printStackTrace
   11: return
  Exception table:
    from  to target type
      0   3   6  Class java/lang/Exception  // 异常表条目

```



##### 处置流程

- 当程序触发异常时，从上到下依次比较异常表的记录
  - 查看异常的发生位置是否在from和to监控的范围之内
  - 如果在就查看异常表的类型是否和抛出的异常一致
- 如果没有找到，会弹出当前方法的栈帧，然后在调用者的异常表中做上面的比较



极端情况下，需要弹出当前线程的所有栈帧，并完成比较。



##### finally代码块

- 把finally代码块的执行内容，放到try-catch的所有正常的和异常的出口处

![img](https://static001.geekbang.org/resource/image/17/06/17e2a3053b06b0a4383884f106e31c06.png)

```java

public class Foo {
  private int tryBlock;
  private int catchBlock;
  private int finallyBlock;
  private int methodExit;

  public void test() {
    try {
      tryBlock = 0;
    } catch (Exception e) {
      catchBlock = 1;
    } finally {
      finallyBlock = 2;
    }
    methodExit = 3;
  }
}


$ javap -c Foo
...
  public void test();
    Code:
       0: aload_0
       1: iconst_0
       2: putfield      #20                 // Field tryBlock:I
       5: goto          30
       8: astore_1
       9: aload_0
      10: iconst_1
      11: putfield      #22                 // Field catchBlock:I
      14: aload_0
      15: iconst_2
      16: putfield      #24                 // Field finallyBlock:I
      19: goto          35
      22: astore_2
      23: aload_0
      24: iconst_2
      25: putfield      #24                 // Field finallyBlock:I
      28: aload_2
      29: athrow
      30: aload_0
      31: iconst_2
      32: putfield      #24                 // Field finallyBlock:I
      35: aload_0
      36: iconst_3
      37: putfield      #26                 // Field methodExit:I
      40: return
    Exception table:
       from    to  target type
           0     5     8   Class java/lang/Exception
           0    14    22   any

  ...

```

- finally 代码块会出现三次
  - try正常运行的后面
  - try有异常被catch捕获之后且正常处置的后面
  - try有异常catch没捕获，或者catch执行出现异常的后面
    - 异常表新增了一个异常，异常的检测范围是try-catch代码块，异常的target是finally代码块
- 如果try中抛出了异常A，catch捕获到了A，但是执行的时候抛出了异常B，此时finally捕获到了异常B，然后重抛之后，抛的是异常B



#### try-with-resource

- 实现AutoCloseable类
- jdk1.7之后
- catch可以同时捕获多个







#### 实战

```java
public class Foo {
    private int tryBlock;
    private int catchBlock;
    private int finallyBlock;
    private int methodExit;

    public void test() {
        for (int i = 0; i < 100; i++) {
            try {
                tryBlock = 0;
                if (i < 50) {
                    continue;
                } else if (i < 80) {
                    break;
                } else {
                    return;
                }
            } catch (Exception e) {
                catchBlock = 1;
            } finally {
                finallyBlock = 2;
            }
        }
        methodExit = 3;
    }
}
```



```java
Compiled from "Foo.java"
public class jvm.exception.Foo {
  public jvm.exception.Foo();
    Code:
       0: aload_0
       1: invokespecial #1                  // Method java/lang/Object."<init>":()V
       4: return

  public void test();
    Code:
       0: iconst_0
       1: istore_1
       2: iload_1
       3: bipush        100
       5: if_icmpge     75
       8: aload_0
       9: iconst_0
      10: putfield      #2                  // Field tryBlock:I
      13: iload_1
      14: bipush        50
      16: if_icmpge     27
      19: aload_0
      20: iconst_2
      21: putfield      #3                  // Field finallyBlock:I
      24: goto          69
      27: iload_1
      28: bipush        80
      30: if_icmpge     41
      33: aload_0
      34: iconst_2
      35: putfield      #3                  // Field finallyBlock:I
      38: goto          75
      41: aload_0
      42: iconst_2
      43: putfield      #3                  // Field finallyBlock:I
      46: return
      47: astore_2
      48: aload_0
      49: iconst_1
      50: putfield      #5                  // Field catchBlock:I
      53: aload_0
      54: iconst_2
      55: putfield      #3                  // Field finallyBlock:I
      58: goto          69
      61: astore_3
      62: aload_0
      63: iconst_2
      64: putfield      #3                  // Field finallyBlock:I
      67: aload_3
      68: athrow
      69: iinc          1, 1
      72: goto          2
      75: aload_0
      76: iconst_3
      77: putfield      #6                  // Field methodExit:I
      80: return
    Exception table:
       from    to  target type
           8    19    47   Class java/lang/Exception
          27    33    47   Class java/lang/Exception
           8    19    61   any
          27    33    61   any
          47    53    61   any
}

```

